/**
** Callbacks
Callbacks wrap a target, callable, and arguments to pass. See the dm reference for call().
When the target is GLOBAL_PROC, the callable is global - otherwise it is a datum (or dead) reference.
Callbacks are created with the new keyword via a global alias like:
- var/datum/callback/instance = new Callback(GLOBAL_PROC, GLOBAL_PROC_REF(get_area), someObject)
Callbacks are thin - they should be used with invoke or invoke_async.

** Invocation
invoke and invoke_async call a callable against a target with optional params. They accept either:
invoke(target, callable, params...)
or invoke(<callback>, extra params...)
and return the result of calling those. invoke_async does not wait for an outcome and will return (.)
on the first sleep, and so should be used only where results are not required.

** Callables
Callables are proc names or proc references, with references preferred for safety at compile time.
Generally these should always be wrapped by PROC_REF and friends.

** Timers
Timers accept callbacks as their first argument. For full timer documentation, see the timedevent
datum. For example:
addTimer(new Callback(myMob, TYPE_PROC_REF(/mob/living/drop_l_hand)), 10 SECONDS)
*/

var/global/const/GLOBAL_PROC = FALSE

var/global/const/datum/callback/Callback = /datum/callback


/datum/callback
	var/identity
	var/datum/target = GLOBAL_PROC
	var/callable
	var/list/params


/datum/callback/Destroy()
	target = null
	callable = null
	LAZYCLEARLIST(params)
	return ..()


/datum/callback/New(datum/target, callable, ...)
	src.target = target
	src.callable = callable
	if (length(args) > 2)
		params = args.Copy(3)
	switch (target)
		if (null)
			identity = "(null) [callable]"
		if (FALSE)
			identity = "(global) [callable]"
		else
			identity = "([target.type] \ref[target]) [callable]"


#define INVOKE_BEHAVIOR \
	if (target == GLOBAL_PROC) { \
		var/list/params; \
		if (length(args) > 2) { \
			params = args.Copy(3); \
		} \
		return call(callable)(arglist(params)); \
	} else if (QDELETED(target)) { \
		return; \
	} else if (istype(target)) { \
		var/list/params = list(target.target, target.callable); \
		if (length(target.params)) { \
			params += target.params; \
		} \
		if (length(args) > 1) { \
			params += args.Copy(2); \
		} \
		return invoke(arglist(params)); \
	} else { \
		var/list/params; \
		if (length(args) > 2) { \
			params = args.Copy(3); \
		} \
		return call(target, callable)(arglist(params)); \
	} \


/proc/invoke(datum/callback/target, callable, ...)
	INVOKE_BEHAVIOR


/proc/invoke_async(datum/callback/target, callable, ...)
	set waitfor = FALSE
	INVOKE_BEHAVIOR


#undef INVOKE_BEHAVIOR
